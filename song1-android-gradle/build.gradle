buildscript {
    repositories {
        mavenCentral()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath 'me.tatarka:gradle-retrolambda:1.3.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.2'

    }
}
apply plugin: 'android'
apply plugin: 'retrolambda'
apply plugin: 'android-apt'

repositories {
    maven { url "http://dl.bintray.com/populov/maven" }
    mavenCentral()

}

/*
 *  Gets the version name from the latest Git tag
 */
def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', 'HEAD', '--count'
            standardOutput = code
        }
        return code.toString().trim().toInteger()
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

def appendVersionNameVersionCode(variant, defaultConfig) {
    //check if staging variant
    if(variant.name == android.buildTypes.staging.name){
        if(variant.zipAlign) {
            def file = variant.outputFile
            def fileName = file.name.replace(".apk", "-" + defaultConfig.versionName + "-" + defaultConfig.versionCode + ".apk")
            variant.outputFile = new File(file.parent, fileName)
        }

        def file = variant.packageApplication.outputFile
        def fileName = file.name.replace(".apk", "-" + defaultConfig.versionName + "-" + defaultConfig.versionCode + ".apk")
        variant.packageApplication.outputFile = new File(file.parent, fileName)
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode getVersionCode()
        versionName getVersionName()
    }

    signingConfigs {
        song1Config {
            storeFile file('keystore.jks')
            storePassword "SZyinyue1hao"
            keyAlias "song1"
            keyPassword "SZyinyue1hao"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            signingConfig signingConfigs.song1Config

            applicationVariants.all { variant ->
                def file = variant.outputFile
                variant.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
            }
        }
    }

    lintOptions {
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:19.1.+'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.guava:guava:16.0.1'
    compile 'com.jakewharton:butterknife:4.0.1'
    compile 'de.akquinet.android.androlog:androlog:1.0.6'
    compile 'com.squareup:otto:1.3.4'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.squareup.picasso:picasso:2.0.0'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.squareup.dagger:dagger:1.+'
    compile 'com.github.kevinsawicki:http-request:5.+'
    compile 'com.viewpagerindicator:library:2.+@aar'
    compile 'com.belerweb:pinyin4j:2.5.0'
    apt 'com.squareup.dagger:dagger-compiler:1.+'

}
